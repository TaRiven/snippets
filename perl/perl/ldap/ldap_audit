#!/usr/local/bin/perl -w
# Copyright (c) 2000 Dustin Sallings <dustin@spy.net>
# $Id: ldap_audit,v 1.12 2000/03/04 00:05:45 dustin Exp $

use strict;

use Net::LDAP;
use Data::Dumper;
use LDAPSlave;

# Where to put the configuration
my(%config)=();
# Ignore patterns (if available)
my(@ignore)=();

my($configfile);
if(@ARGV) {
	$configfile=$ARGV[0];
} else {
	$configfile='audit.conf';
}

read_config($configfile);

my $master = Net::LDAP->new($config{'ldap_server'}) or die "$@";

my $res=$master->bind ( dn => $config{'binddn'}, password => $config{'bindpw'});
if($res->code() != 0) {
	print STDERR "ERROR:  Unable to bind to master, giving up, check pw\n";
	exit(-1);
}

my @slaves=get_slaves();

audit($config{'base'}, $master, @slaves);

$master->unbind;
for(@slaves) { $_->ldap()->unbind };

sub audit {
	my($base, $master, @slaves)=@_;
	my($slave);

	my @master_dns=getDns($master, $base);
	my %master_hash=map { $_, 1 } @master_dns;

	# First, just compare the DNs both ways.
	for $slave (@slaves) {
		my @dns=getDns($slave->ldap(), $base);
		my %h=map { $_, 1 } @dns;
		@dns=();

		for(keys(%h)) {
			if(!defined($master_hash{$_})) {
				print "!!! $_ is not in the master !!!\n";
				if($config{'fixit'}==1) {
					print "Fixing.\n";
					$slave->ldap()->delete($_);
				}
			}
		}
		for(keys(%master_hash)) {
			if(!defined($h{$_})) {
				my $t=$slave->name();
				print "!!! $_ is not in the slave ($t) !!!\n";
				if($config{'fixit'}==1) {
					print "Fixing\n";
					my(%h)=gettree($master, $_);
					addEntry($slave->ldap(), %h);
				}
			}
		}
	}

	# Now do a compare on the data in each DN.
	my $dn;
	for $dn (@master_dns) {
		# We want a string and a hash of each.
		my $master_data="";
		my %master_data=();
		my $t="";
		eval {
			%master_data=gettree($master, $dn);
			$master_data=Dumper(\%master_data);
		};
		if($@) {
			print "Failed on $dn\n";
		}
		for $slave (@slaves) {
			$t = $slave->name();
			eval {
				my %slave_data=gettree($slave->ldap(), $dn);
				my $slave_data=Dumper(\%slave_data);

				if($master_data ne $slave_data) {
					# Free the Dumper output.
					print "!!! $dn is not the same on $t !!!\n";
					if($config{'debug'}>0) {
						# print "MASTER:\n$master_data\nSLAVE:\n$slave_data\n";
					}
					$slave_data="";
					if($config{'fixit'}==1) {
						# find and fix the differences
						print "Fixing\n";
						eval {
							fixRecord($slave->ldap(),
								\%master_data, \%slave_data);
						};
						if($@) {
							print "Failed to fix record:  $@\n";
						}
					}
				}
			};

			if($@) {
				print "Failed for $dn on $t, missing? ($@)\n";
			}
		}
	}
}

sub gettree {
	my($ldap, $base)=@_;
	my($result, $entry, $att, $ret);

	$ret="";

	$result = $ldap->search ( 'base' => $base, 'scope' => 'base',
		'filter' => '(objectclass=*)',
	);

	$result->code && die $result->error;

	my %h=();
	for $entry ($result->all_entries()) {
		my $dn=$entry->dn();
		$h{$dn}{'dn'}=$dn;
		for $att (sort($entry->attributes())) {
			my(@vals);
			@vals=sort($entry->get($att));

			$h{$dn}{$att}=\@vals;
		}
	}

	return(%h);
}

sub getDns {
	my($ldap, $base)=@_;
	my($result, $entry, @ret);
	@ret=();
	$result = $ldap->search ( 'base' => $base, 'scope' => 'sub',
		'filter' => '(objectclass=*)', 'attrs' => ['dn']
	);

	if($result->code) {
		print STDERR "Error getting DNs: " . $result->error . "\n";
		die $result->error;
	}

	for $entry ($result->all_entries()) {
		push(@ret, $entry->dn());
	}
	return(sort(@ret));
}

sub fixRecord {
	my($slave_ldap, $master, $slave)=@_;
	# the dn is the key, the values contains the actual attributes and
	# values
	my($mtmp)=values(%$master);
	my($stmp)=values(%$slave);
	my(%master)=%$mtmp;
	my(%slave)=%$stmp;

	my $dn = $master{'dn'};

	my @add=();
	my @deletes=();

	my $key;
	my(@replace) = ();

	# Flip through the master and verify attributes here.
	for $key (keys(%master)) {
		# skip the DN
		next if($key eq "dn");
		my @m=@{$master{$key}};
		my @s=();
		# Only do this if it's defined, else it'll get added anyway
		if(defined($slave{$key})) {
			@s=@{$slave{$key}};
		} else {
			# Skip the ``always'' ones.
			next if($_ eq "creatorsname");
			next if($_ eq "createtimestamp");
			next if($_ eq "modifytimestamp");
			next if($_ eq "modifiersname");

			# The attribute was not defined, so we need to add the whole thing.
			print "Replacing $key\n";
			push(@replace, [ $key, $master{$key} ] );
			next;
		}
		my %mh=map { $_, 1}@m;
		my %sh=map { $_, 1}@s;
		# First, check for values that're in the master, but not the slave.
		for(keys %mh) {
			next if($_ eq "creatorsname");
			next if($_ eq "createtimestamp");
			next if($_ eq "modifytimestamp");
			next if($_ eq "modifiersname");
			if(!defined($sh{$_})) {
				print "Attribute $key with $_ is not in the slave:\n";
				my $patcheck="attribute=$key=$_";
				my $match=0;
				for(@ignore) {
					if($patcheck=~/$_/i) {
						print "Matched pattern $_, ignoring\n";
						$match=1;
						next;
					}
				}
				if($match==0) {
					push(@add, [$key, $_ ]);
				}
			}
		}
		# Now do it the other way around
		for(keys %sh) {
			next if($_ eq "creatorsname");
			next if($_ eq "createtimestamp");
			next if($_ eq "modifytimestamp");
			next if($_ eq "modifiersname");
			if(!defined($mh{$_})) {
				print "Attribute $key with $_ is not in the master:\n";
				my $patcheck="attribute=$key=$_";
				my $match=0;
				for(@ignore) {
					if($patcheck=~/$_/i) {
						print "Matched pattern $_, ignoring\n";
						$match=1;
						next;
					}
				}
				if($match==0) {
					push(@deletes, [$key, [$_] ]);
				}
			}
		}
	}

	# Get rid of stuff that's on the slave, but not in the master.
	for $key (keys(%slave)) {
		if(!defined($master{$key})) {
			push(@deletes, [ $key, [] ]);
		}
	}

	# we always have to modify these.
	for('creatorsname', 'createtimestamp', 'modifiersname', 'modifytimestamp'){
		if(defined($master{$_})) {
			push(@replace, [ $_, $master{$_} ]);
		}
	}

	my @mods=();
	for(@replace) {
		push(@mods, 'replace', $_);
	}
	for(@deletes) {
		push(@mods, 'delete', $_);
	}
	for(@add) {
		push(@mods, 'add', $_);
	}

	# do the modify
	print Dumper(\@mods) if($config{'debug'}>1);
	my $res=$slave_ldap->modify($dn, 'changes' => \@mods );
	if($res->code()!=0) {
		print "WARNING!  Modify of $dn failed:  " . $res->code() . "\n";
	}
}

sub addEntry {
	my($slave_ldap, %entry)=@_;

	# Get the values past the dn
	my($mtmp)=values(%entry);
	%entry=%$mtmp;

	my $dn=$entry{'dn'};

	my(@attr)=();
	for(keys(%entry)) {
		next if($_ eq 'dn');
		push(@attr, $_, $entry{$_});
	}

	my $res=$slave_ldap->add('dn' => $dn, 'attr' => \@attr);
	if($res->code()!=0) {
		print "WARNING!  Add of $dn failed:  " . $res->code() . "\n";
	}
}

sub get_slaves {
	my(@slaves);
	for(split(/\s*,\s*/, $config{'slave_servers'})) {
		my($hostname, $binddn, $bindpw);

		if(defined($config{'slave.' . $_ . '.hostname'})) {
			$hostname=$config{'slave.' . $_ . '.hostname'};
		} else {
			$hostname=$_;
		}

		if(defined($config{'slave.' . $_ . '.binddn'})) {
			$binddn=$config{'slave.' . $_ . '.binddn'};
		} else {
			$binddn=$config{'binddn'};
		}

		if(defined($config{'slave.' . $_ . '.bindpw'})) {
			$bindpw=$config{'slave.' . $_ . '.bindpw'};
		} else {
			$bindpw=$config{'bindpw'};
		}

		my $tmp=Net::LDAP->new($hostname, 'Debug' => 3);
		if(!$tmp) {
			print "WARNING!  Failed to connect to $hostname, ignoring.\n";
			next;
		}

		my $res=$tmp->bind( dn => $binddn, password => $bindpw);
		if($res->code()==0) {
			push(@slaves, new LDAPSlave($_, $tmp));
		} else {
			print "WARNING!  Bind to $hostname failed, ignoring.\n";
		}
	}
	return(@slaves);
}

sub read_config {
	my($file)=@_;

	open(IN, $file) || die("Can't open $file:  $!\n");
	while(<IN>) {
		next if(/^#/);
		next unless(/\w/);
		chomp;
		my(@a)=split(/\s*=\s*/, $_, 2);

		$config{$a[0]}=$a[1];
	}
	close(IN);

	if(defined($config{'ignore_pat'})) {
		open(IN, "<$config{'ignore_pat'}")
			|| print "WARNING:  Can't open ignore file.\n";
		while(<IN>) {
			chomp;
			push(@ignore, $_);
		}
		close(IN);
	}

	print "Loaded " . @ignore . " ignore patterns.\n" if($config{'debug'});
}
